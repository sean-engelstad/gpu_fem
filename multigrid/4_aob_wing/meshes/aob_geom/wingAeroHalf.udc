# Generate wing aerodynamic component

interface rootChord in 5.0
interface tipChord in 1.5
interface halfSpan in 14.0
interface tipOffset in 7.5
interface symPlane in 1
interface canMirror in 1

interface nStations in 9

interface twist1 in 0
interface twist2 in 0
interface twist3 in 0
interface twist4 in 0
interface twist5 in 0
interface twist6 in 0
interface twist7 in 0
interface twist8 in 0
interface twist9 in 0

set xFarfield 300

# Make the full wing solid
udparg $/wingSolid nStations nStations
udparg $/wingSolid rootChord rootChord tipChord tipChord halfSpan halfSpan tipOffset tipOffset
udparg $/wingSolid twist1 twist1 twist2 twist2 twist3 twist3 twist4 twist4
udprim $/wingSolid twist5 twist5 twist6 twist6 twist7 twist7 twist8 twist8 twist9 twist9
store wingSolid


ifthen symPlane EQ 1
  box -xFarfield 0 -xFarfield 2*xFarfield xFarfield 2*xFarfield
  select face
    attribute tagComp $farfield
  select face 3
    attribute tagComp $symmetry
else
  #sphere 0 0 0 xFarfield
  box -xFarfield -xFarfield -xFarfield 2*xFarfield 2*xFarfield 2*xFarfield
  #rotatex 35 0 0
  #rotatey 35 0 0
  select face
    attribute tagComp $farfield
endif

restore wingSolid

ifthen canMirror EQ 1
   mirror 0 -1 0
   restore wingSolid
   union
   #throw 999
endif

subtract

ifthen symPlane EQ 1
   udprim editAttr filename <<
     EDGE ADJ2FACE tagComp1=upperSurface
     AND ADJ2FACE tagComp=symmetry
     SET tagComp=rootUpperEdge
     SET capsMesh=rootUpperEdgeMesh

     EDGE ADJ2FACE tagComp1=lowerSurface
     AND ADJ2FACE tagComp=symmetry
     SET tagComp=rootlowerEdge
     SET capsMesh=rootLowerEdgeMesh

     EDGE ADJ2FACE tagComp1=upperSurface
     AND ADJ2FACE tagComp1=tipSurface
     SET tagComp=tipUpperEdge
     SET capsMesh=tipUpperEdgeMesh

     EDGE ADJ2FACE tagComp1=lowerSurface
     AND ADJ2FACE tagComp1=tipSurface
     SET tagComp=tiplowerEdge
     SET capsMesh=tipLowerEdgeMesh
   >>
else
   udprim editAttr filename <<
     EDGE ADJ2FACE tagComp1=upperSurface
     AND ADJ2FACE tagComp1=rootSurface
     SET tagComp=rootUpperEdge
     SET capsMesh=rootUpperEdgeMesh

     EDGE ADJ2FACE tagComp1=lowerSurface
     AND ADJ2FACE tagComp1=rootSurface
     SET tagComp=rootlowerEdge
     SET capsMesh=rootLowerEdgeMesh

     EDGE ADJ2FACE tagComp1=upperSurface
     AND ADJ2FACE tagComp1=tipSurface
     SET tagComp=tipUpperEdge
     SET capsMesh=tipUpperEdgeMesh

     EDGE ADJ2FACE tagComp1=lowerSurface
     AND ADJ2FACE tagComp1=tipSurface
     SET tagComp=tiplowerEdge
     SET capsMesh=tipLowerEdgeMesh
   >>
endif

select face $tagComp1 $tipSurface
  attribute capsMesh $tipMesh
  attribute capsGroup $wing
  attribute AFLR4_CMP_ID 2
  attribute AFLR_GBC $-STD_UG3_GBC

select face $tagComp1 $upperSurface
select add $tagComp1 $lowerSurface
  attribute capsMesh $wingMesh
  attribute capsGroup $wing
  attribute AFLR4_CMP_ID 2
  attribute AFLR_GBC $-STD_UG3_GBC

ifthen symPlane EQ 0 AND canMirror EQ 0
   select face $tagComp1 $rootSurface
  attribute capsMesh $wingMesh
  attribute capsGroup $wing
  attribute AFLR4_CMP_ID 2
  attribute AFLR_GBC $-STD_UG3_GBC
endif

select face $tagComp1 $teSurface
  attribute capsMesh $teMesh
  attribute capsGroup $wing
  attribute AFLR4_CMP_ID 2
  attribute AFLR_GBC $-STD_UG3_GBC


select face $tagComp $farfield
  attribute capsMesh $farfieldMesh
  attribute capsGroup $farfield
  attribute AFLR_GBC $FARFIELD_UG3_GBC
  attribute AFLR4_CMP_ID 3
  attribute AFLR4_Scale_Factor 10.0

ifthen symPlane EQ 1
   select face $tagComp $symmetry
     attribute capsMesh $symmetryMesh
     attribute capsGroup $symmetry
     attribute AFLR4_CMP_ID 1
     attribute AFLR_GBC $BL_INT_UG3_GBC
endif

ifthen symPlane EQ 1
   udprim editAttr filename <<
     EDGE ADJ2FACE capsMesh=farfieldMesh
     SET tagComp=farfieldEdge
     SET capsMesh=farfieldEdgeMesh

     EDGE ADJ2FACE tagComp=symmetry
     AND ADJ2FACE tagComp1=teSurface
     SET tagEdge=teRoot
   >>
else
   udprim editAttr filename <<
     EDGE ADJ2FACE capsMesh=farfieldMesh
     SET tagComp=farfieldEdge
     SET capsMesh=farfieldEdgeMesh

     EDGE ADJ2FACE tagComp1=rootSurface
     AND ADJ2FACE tagComp1=teSurface
     SET tagEdge=teRoot
   >>
endif


select edge $tagEdge $teUp
select add $tagEdge $teLow
  attribute capsMesh $teHorizEdgeMesh

select edge $tagEdge $teRoot
select add $tagEdge $teTip
  attribute capsMesh $teVertEdgeMesh

select edge $tagEdge $leEdge
  attribute capsMesh $leEdgeMesh

ifthen canMirror EQ 0
   select face $tagComp1 $rootSurface
     attribute capsMesh $rootMesh
endif


select body
  attribute capsAIM $fun3dAIM;aflr3AIM;egadsTessAIM;aflr4AIM
  attribute capsMeshLength 1.0

end

|||||